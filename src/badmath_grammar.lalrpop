use crate::badmath::Term;
use crate::badmath::binop;

grammar;

pub V1Terms = V1Term*;

V1Term = Tier<Op,ParenTerm<V1Term,NumTerm>>;

pub V2Terms = MulTerm*;

MulTerm : Term = Tier<MulOp,AddTerm>;
AddTerm : Term = Tier<AddOp,ParenTerm<MulTerm,NumTerm>>;

ParenTerm<TopTier,NextTier> : Term = {
    "(" <TopTier> ")" => <>,
    NextTier,
};

NumTerm : Term = <NumDec> => Term::Num(<>);

Tier<Op,NextTier>: Term = {
    Tier<Op,NextTier> Op NextTier => binop(<>),
    NextTier
}; 

Op = {AddOp,MulOp};
AddOp: char = <o:r"[\+]"> => o.chars().nth(0).unwrap();
MulOp: char = <o:r"[\*]"> => o.chars().nth(0).unwrap();

NumDec: i64 = <n:r"[0-9]+"> => n.parse().unwrap();